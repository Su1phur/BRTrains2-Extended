// purchase sprite
switch(FEAT_TRAINS, PARENT, sw_BR466_purchase_sprite, current_year) {
    1969..1996: spriteset_BR465_DMOS_NSE_purchase;
    1997..2003: spriteset_BR465_DMOS_Connex_purchase;
        spriteset_BR465_DMOS_Southeastern_purchase; }

// graphics switch
switch(FEAT_TRAINS, SELF, sw_graphics_BR466_NSE, position_in_articulated_veh % 6) {
	1: return sw_loadstack_BR465_DMOS_NSE;
    4: return sw_loadstack_BR465_DMOS_NSE_REV;
        spriteset_empty; }

switch(FEAT_TRAINS, SELF, sw_graphics_BR466_Connex, position_in_articulated_veh % 6) {
	1: return sw_loadstack_BR465_DMOS_Connex;
    4: return sw_loadstack_BR465_DMOS_Connex_REV;
        spriteset_empty; }

switch(FEAT_TRAINS, SELF, sw_graphics_BR466_SET, position_in_articulated_veh % 6) {
	1: return sw_loadstack_BR465_DMOS_SET;
    4: return sw_loadstack_BR465_DMOS_SET_REV;
        spriteset_empty; }

switch(FEAT_TRAINS, SELF, sw_graphics_BR466_Southeastern, position_in_articulated_veh % 6) {
	1: return sw_loadstack_BR465_DMOS_Southeastern;
    4: return sw_loadstack_BR465_DMOS_Southeastern_REV;
        spriteset_empty; }

// livery switch
switch(FEAT_TRAINS, SELF, sw_livery_BR466, cargo_subtype) {
    0: sw_graphics_BR466_NSE;
    1: sw_graphics_BR466_Connex;
    2: sw_graphics_BR466_SET;
    3: sw_graphics_BR466_Southeastern; }

// livery name refit switch
switch(FEAT_TRAINS, SELF, sw_BR466_cargo_subtype_text, cargo_subtype){
	0:	return string(str_NSE);
    1:  return string(str_Connex);
    2:  return string(str_SET);
    3:  return string(str_Southeastern);
       return CB_RESULT_NO_TEXT; }

// length switch
switch(FEAT_TRAINS, SELF, sw_length_BR466, position_in_articulated_veh % 6) {
	1: 5;
    4: 5;
        4; }

// capacity switch
switch(FEAT_TRAINS, SELF, sw_capacity_BR466, position_in_vehid_chain % 6) {
    0: 163 * param_pax; // all the capacity - (vehid chain number)
    1..5: 1 * param_pax;
    // total 168
}

// articulation switch
switch(FEAT_TRAINS, SELF, sw_articulated_BR466, extra_callback_info1) {
	1..5: return item_BR466;
		return CB_RESULT_NO_MORE_ARTICULATED_PARTS; }


// unit definition
item (FEAT_TRAINS, item_BR466, 4660) {
    property {
        // common properties
        name:                           string(STR_NAME_BR466);
        // variant_group:                  ; // unit ID or item
		// extra_flags:                    bitmask(VEHICLE_FLAG_DISABLE_NEW_VEHICLE_MESSAGE, VEHICLE_FLAG_DISABLE_EXCLUSIVE_PREVIEW, VEHICLE_FLAG_SYNC_VARIANT_EXCLUSIVE_PREVIEW, VEHICLE_FLAG_SYNC_VARIANT_RELIABILITY);
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1993,01,01); 
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life: 					40;
        retire_early: 					-10;							
        reliability_decay: 				20;
        
		refittable_cargo_classes:        bitmask(CC_PASSENGERS);
        // non_refittable_cargo_classes:    bitmask();
        cargo_allow_refit:               [PASS, TOUR];
        // cargo_disallow_refit:            [LVST];
        misc_flags: 					bitmask(TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_SPRITE_STACK);
        cargo_capacity:                 1;
        loading_speed:					15;
        cost_factor: 					108;
        running_cost_base:			    RUNNING_COST_ELECTRIC;					
        running_cost_factor: 			90;

        //specific properties
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          75 mph;
        refit_cost:                     0;
        track_type:                     BR_3RDR;
        power:                          1500 hp;
        weight:                         72 ton;
        engine_class:                   ENGINE_CLASS_ELECTRIC;
        dual_headed:                    0;
        air_drag_coefficient:           0.11;
        tractive_effort_coefficient:    0.08;
        length:                         8;
    }

    graphics {
        // graphics
        default:                        sw_livery_BR466;
        purchase:                       sw_BR466_purchase_sprite;
        sound_effect:                   sw_networker_sound;

        // internal code
        length:                         sw_length_BR466;
        articulated_part:               sw_articulated_BR466;
        purchase_cargo_capacity:        return (28 * param_pax); // divide by number of articulated parts and floor
        cargo_capacity:                 sw_capacity_BR466;
        cost_factor:                    193;
        running_cost_factor:            5261;
        cargo_subtype_text:             sw_BR466_cargo_subtype_text; 
        // power:                          sw_power_BR466;
        colour_mapping:                 return PALETTE_CC_FIRST;
        // colour_mapping:                 switch_generic_recolour;

        // purchase menu text
        additional_text:                return( string(str_purchase_loco_with_liveries,
                                                    string(str_purchase_type_emu),
                                                    string(str_usage_pax),
                                                    string(str_BR465_466_liveries)));
    }
}

