// basic sprites
spriteset(spriteset_BR_Conflat_A_BR_Bauxite, "gfx\Wagons\BR_Conflat_A\BR_Conflat_A_BR_Bauxite_1x_8bpp.png")
    { template_48trains_1x(0,0) }

    alternative_sprites(spriteset_BR_Conflat_A_BR_Bauxite, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "gfx\Wagons\BR_Conflat_A\BR_Conflat_A_BR_Bauxite_1x_32bpp.png", "gfx\Wagons\BR_Conflat_A\BR_Conflat_A_BR_Bauxite_1x_mask.png")
    { template_48trains_1x(0,0) }

    alternative_sprites(spriteset_BR_Conflat_A_BR_Bauxite, ZOOM_LEVEL_IN_2X, BIT_DEPTH_32BPP, "gfx\Wagons\BR_Conflat_A\BR_Conflat_A_BR_Bauxite_2x_32bpp.png", "gfx\Wagons\BR_Conflat_A\BR_Conflat_A_BR_Bauxite_2x_mask.png")
    { template_48trains_2x(0,0) }

    alternative_sprites(spriteset_BR_Conflat_A_BR_Bauxite, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP, "gfx\Wagons\BR_Conflat_A\BR_Conflat_A_BR_Bauxite_4x_32bpp.png", "gfx\Wagons\BR_Conflat_A\BR_Conflat_A_BR_Bauxite_4x_mask.png")
    { template_48trains_4x(0,0) }

// purchase sprite
spriteset(spriteset_BR_Conflat_A_purchase, "gfx\Wagons\BR_Conflat_A\BR_Conflat_A_BR_Bauxite_1x_8bpp.png")
    { template_purchase(0,0) }

    alternative_sprites(spriteset_BR_Conflat_A_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "gfx\Wagons\BR_Conflat_A\BR_Conflat_A_BR_Bauxite_1x_32bpp.png", "gfx\Wagons\BR_Conflat_A\BR_Conflat_A_BR_Bauxite_1x_mask.png")
    { template_purchase(0,0) }

// livery switch
switch(FEAT_TRAINS, SELF, sw_livery_BR_Conflat_A, cargo_subtype) {
    0: spriteset_BR_Conflat_A_BR_Bauxite;
}

// livery name refit switch
switch(FEAT_TRAINS, SELF, sw_BR_Conflat_A_cargo_subtype_text, cargo_subtype){
	0:	return string(str_BRBauxite);
       return CB_RESULT_NO_TEXT;
}

// container switches
switch(FEAT_TRAINS, SELF, sw_loadstack_BR_Conflat_A_Any_Centered, [STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8)]) {
    0: sw_livery_BR_Conflat_A;
    1: random_BR_Container_Any_Centered; }

switch(FEAT_TRAINS, SELF, sw_loadstack_BR_Conflat_A_Cold_Centered, [STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8)]) {
    0: sw_livery_BR_Conflat_A;
    1: random_BR_Container_Cold_Centered; }

switch(FEAT_TRAINS, SELF, sw_BR_Conflat_A_cargo_type, cargo_type_in_veh) {
    FISH: return sw_loadstack_BR_Conflat_A_Cold_Centered;
    FOOD: return sw_loadstack_BR_Conflat_A_Cold_Centered;
    FRUT: return sw_loadstack_BR_Conflat_A_Cold_Centered;
    FRVG: return sw_loadstack_BR_Conflat_A_Cold_Centered;
    MILK: return sw_loadstack_BR_Conflat_A_Cold_Centered;

    sw_loadstack_BR_Conflat_A_Any_Centered;
}


// unit definition
item (FEAT_TRAINS, item_BR_Conflat_A, 12200) { // Diagram 61/62 12/13 ton container flat 
    property {
        // common properties
        name:                           string(STR_NAME_BR_CONFLAT_A);
        // variant_group:                  ; // unit ID or item

        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1950,01,01); 
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life: 					40;
        retire_early: 					-10;							
        reliability_decay: 				20;
		refittable_cargo_classes:		bitmask(CC_PIECE_GOODS, CC_REFRIGERATED, CC_NON_POURABLE);	
        non_refittable_cargo_classes:   bitmask(CC_BULK);
        cargo_disallow_refit:           [LVST, WATR, BEER, IRON, VEHI];
        misc_flags: 					bitmask(TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_SPRITE_STACK);
        cargo_capacity:                 13 * param_freight;
        loading_speed:					5;
        cost_factor: 					108;			
        running_cost_base:				RUNNING_COST_STEAM;				
        running_cost_factor: 			90;

        //specific properties
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          50 mph;
        refit_cost:                     0;
        track_type:                     RAIL;
        weight:                         6 ton;
        dual_headed:                    0;
        air_drag_coefficient:           0.05;
        tractive_effort_coefficient:    0.3;
        length:                         4;
    }

    graphics {
        // graphics
        default:                        sw_BR_Conflat_A_cargo_type;
        purchase:                       spriteset_BR_Conflat_A_purchase;
        
        // internal code
        cost_factor:                    725;
        running_cost_factor:            646;
        cargo_subtype_text:             sw_BR_Conflat_A_cargo_subtype_text; 
        colour_mapping:                 return PALETTE_CC_FIRST;

        // purchase menu text
        additional_text:                return(string(str_purchase_wagon,
                                            string(str_BR_Conflat_A_liveries))
                                        );
    }
}