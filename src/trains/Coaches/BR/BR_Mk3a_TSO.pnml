// basic sprites
spriteset(spriteset_BR_Mk3_TS_BR_ScotRail, "gfx/Coaches/BR_Mk3_TS/BR_Mk3_TS_BR_ScotRail_1x_8bpp.png")
    { template_148trains_1x(0,0) }

    alternative_sprites(spriteset_BR_Mk3_TS_BR_ScotRail, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "gfx/Coaches/BR_Mk3_TS/BR_Mk3_TS_BR_ScotRail_1x_32bpp.png", "gfx/Coaches/BR_Mk3_TS/BR_Mk3_TS_BR_ScotRail_1x_mask.png")
    { template_148trains_1x(0,0) }

    alternative_sprites(spriteset_BR_Mk3_TS_BR_ScotRail, ZOOM_LEVEL_IN_2X, BIT_DEPTH_32BPP, "gfx/Coaches/BR_Mk3_TS/BR_Mk3_TS_BR_ScotRail_2x_32bpp.png", "gfx/Coaches/BR_Mk3_TS/BR_Mk3_TS_BR_ScotRail_2x_mask.png")
    { template_148trains_2x(0,0) }

    alternative_sprites(spriteset_BR_Mk3_TS_BR_ScotRail, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP, "gfx/Coaches/BR_Mk3_TS/BR_Mk3_TS_BR_ScotRail_4x_32bpp.png", "gfx/Coaches/BR_Mk3_TS/BR_Mk3_TS_BR_ScotRail_4x_mask.png")
    { template_148trains_4x(0,0) }

spriteset(spriteset_BR_Mk3_TS_BR_ScotRail_Loading, "gfx/Coaches/BR_Mk3_TS/BR_Mk3_TS_BR_ScotRail_Loading_1x_8bpp.png")
    { template_148trains_1x(0,0) }

    alternative_sprites(spriteset_BR_Mk3_TS_BR_ScotRail_Loading, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP, "gfx/Coaches/BR_Mk3_TS/BR_Mk3_TS_BR_ScotRail_Loading_1x_32bpp.png", "gfx/Coaches/BR_Mk3_TS/BR_Mk3_TS_BR_ScotRail_Loading_1x_mask.png")
    { template_148trains_1x(0,0) }

    alternative_sprites(spriteset_BR_Mk3_TS_BR_ScotRail_Loading, ZOOM_LEVEL_IN_2X, BIT_DEPTH_32BPP, "gfx/Coaches/BR_Mk3_TS/BR_Mk3_TS_BR_ScotRail_Loading_2x_32bpp.png", "gfx/Coaches/BR_Mk3_TS/BR_Mk3_TS_BR_ScotRail_Loading_2x_mask.png")
    { template_148trains_2x(0,0) }

    alternative_sprites(spriteset_BR_Mk3_TS_BR_ScotRail_Loading, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP, "gfx/Coaches/BR_Mk3_TS/BR_Mk3_TS_BR_ScotRail_Loading_4x_32bpp.png", "gfx/Coaches/BR_Mk3_TS/BR_Mk3_TS_BR_ScotRail_Loading_4x_mask.png")
    { template_148trains_4x(0,0) }

// loading sprite
spritegroup spritegroup_BR_Mk3_TS_BR_ScotRail_Loading {  
    loaded: [spriteset_empty];
    loading: [spriteset_BR_Mk3_TS_BR_ScotRail_Loading]; }

// sprite stacking
switch(FEAT_TRAINS, SELF, sw_loadstack_BR_Mk3a_TSO_BR_Blue, [ STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 2 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8) ] ) {
    0: spriteset_BR_Mk3_TS_BR_Blue;
    1: spritegroup_BR_Mk3_TS_BR_Blue_Loading;
    2: spriteset_BR_Mk3a_Buffers;
}

switch(FEAT_TRAINS, SELF, sw_loadstack_BR_Mk3a_TSO_Intercity_Executive, [ STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 2 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8) ] ) {
    0: spriteset_BR_Mk3_TS_Intercity_Executive;
    1: spritegroup_BR_Mk3_TS_BR_Blue_Loading;
    2: spriteset_BR_Mk3a_Buffers;
}

switch(FEAT_TRAINS, SELF, sw_loadstack_BR_Mk3a_TSO_Intercity_Swallow, [ STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 2 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8) ] ) {
    0: spriteset_BR_Mk3_TS_Intercity_Swallow;
    1: spritegroup_BR_Mk3_TS_BR_Blue_Loading;
    2: spriteset_BR_Mk3a_Buffers;
}

switch(FEAT_TRAINS, SELF, sw_loadstack_BR_Mk3a_TSO_BR_ScotRail, [ STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 2 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8) ] ) {
    0: spriteset_BR_Mk3_TS_BR_ScotRail;
    1: spritegroup_BR_Mk3_TS_BR_ScotRail_Loading;
    2: spriteset_BR_Mk3a_Buffers;
}

// purchase sprite stacking
switch(FEAT_TRAINS, SELF, sw_spritestack_BR_Mk3a_TSO_BR_Blue_purchase, [STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8)]) {
    0: spriteset_BR_Mk3_TS_BR_Blue_purchase;
    1: spriteset_BR_Mk3a_Buffers_purchase;
}

switch(FEAT_TRAINS, SELF, sw_spritestack_BR_Mk3a_TSO_BR_Intercity_purchase, [STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100), getbits(extra_callback_info1, 8, 8)]) {
    0: spriteset_BR_Mk3_TS_Intercity_Swallow_purchase;
    1: spriteset_BR_Mk3a_Buffers_purchase;
}

// livery switch
switch(FEAT_TRAINS, SELF, sw_livery_BR_Mk3a_TSO, cargo_subtype) {
    0: sw_loadstack_BR_Mk3a_TSO_BR_Blue;
    1: sw_loadstack_BR_Mk3a_TSO_Intercity_Executive;
    2: sw_loadstack_BR_Mk3a_TSO_Intercity_Swallow;
    3: sw_loadstack_BR_Mk3a_TSO_BR_ScotRail; }

// livery name refit switch
switch(FEAT_TRAINS, SELF, sw_BR_Mk3a_TSO_cargo_subtype_text, cargo_subtype){
	0:	return string(str_BRBlueGrey);
    1:  return string(str_BRInterCityExecutive);
    2:  return string(str_BRInterCitySwallow);
    3:  return string(str_BRScotRail);
       return CB_RESULT_NO_TEXT; }

// graphics switch
switch(FEAT_TRAINS, SELF, sw_graphics_BR_Mk3a_TSO, position_in_articulated_veh % 3) {
	0: spriteset_empty;
	1: return sw_livery_BR_Mk3a_TSO;
	2: spriteset_empty;
}

// purchase year switch
switch(FEAT_TRAINS, PARENT, sw_BR_Mk3a_TSO_purchase_sprite, current_year) {
    1965..1982: sw_spritestack_BR_Mk3a_TSO_BR_Blue_purchase;
    1983..1993: sw_spritestack_BR_Mk3a_TSO_BR_Intercity_purchase;
        sw_spritestack_BR_Mk3a_TSO_BR_Intercity_purchase;
}

// capacity switch
switch(FEAT_TRAINS, SELF, sw_capacity_BR_Mk3a_TSO, position_in_vehid_chain % 3) {
    0: 69 * param_pax; // all the capacity - (vehid chain number)
    1..2: 1 * param_pax;
    // total 72
}


// articulation switch
switch(FEAT_TRAINS, SELF, sw_articulated_BR_Mk3a_TSO, extra_callback_info1) {
	1: return item_BR_Mk3a_TSO;
	2: return item_BR_Mk3a_TSO;
		return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

// unit definition
item (FEAT_TRAINS, item_BR_Mk3a_TSO, 12142) {
    property {
        // common properties
        name:                           string(STR_NAME_BR_Mk3a_TSO);
        // variant_group:                  ; // unit ID or item
		// extra_flags:                    bitmask(VEHICLE_FLAG_DISABLE_NEW_VEHICLE_MESSAGE, VEHICLE_FLAG_DISABLE_EXCLUSIVE_PREVIEW, VEHICLE_FLAG_SYNC_VARIANT_EXCLUSIVE_PREVIEW, VEHICLE_FLAG_SYNC_VARIANT_RELIABILITY);
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1975,01,01); 
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life: 					40;
        retire_early: 					-10;							
        reliability_decay: 				20;
        
		refittable_cargo_classes:        bitmask(CC_PASSENGERS);
        cargo_allow_refit:               [PASS, TOUR];
        misc_flags: 					bitmask(TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_SPRITE_STACK);
        cargo_capacity:                 1;
        loading_speed:					15;
        cost_factor: 					108;			
        running_cost_base:				RUNNING_COST_STEAM;				
        running_cost_factor: 			90;

        //specific properties
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          125 mph;
        refit_cost:                     0;
        track_type:                     RAIL;
        weight:                         33 ton;
        dual_headed:                    0;
        length:                         8;
    }

    graphics {
        // graphics
        default:                        sw_graphics_BR_Mk3a_TSO;
        purchase:                       sw_BR_Mk3a_TSO_purchase_sprite;
        
        // internal code
        length:                         sw_length_BR_Mk3;
        articulated_part:               sw_articulated_BR_Mk3a_TSO;
        purchase_cargo_capacity:        return (24 * param_pax); // divide by number of articulated parts and floor
        cargo_capacity:                 sw_capacity_BR_Mk3a_TSO;
        cost_factor:                    3000;
        running_cost_factor:            2055;
        cargo_subtype_text:             sw_BR_Mk3a_TSO_cargo_subtype_text; 
        colour_mapping:                 return PALETTE_CC_FIRST;

        // purchase menu text
        additional_text:                return( string(str_purchase_coach,
                                                    string(str_coach_TSO),
                                                    string(str_BR_Mk3a_liveries)));
    }
}